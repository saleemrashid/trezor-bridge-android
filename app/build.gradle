apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.saleemrashid.trezor.bridge"
        minSdkVersion 12
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        /* Version of https://github.com/trezor/trezord to pretend to be */
        buildConfigField "String", "TREZOR_BRIDGE_VERSION", '"1.2.0"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            proto.srcDir "../vendor/trezor-common/protob"
        }
    }

    protobuf {
        plugins {
            javalite.artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }

        generateProtoTasks {
            all().each { task ->
                task.plugins {
                    javalite {}
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    /* Android Support Libraries */
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:customtabs:25.1.0'
    compile 'com.android.support:design:25.1.0'

    /* Creating KeyStore from SSL certificates */
    compile 'org.bouncycastle:bcpkix-jdk15on:1.55'

    /* Downloading SSL certificates */
    compile 'com.squareup.okhttp3:okhttp:3.5.0'

    /* HTTP Server */
    compile 'org.nanohttpd:nanohttpd:2.3.1'
    compile 'org.nanohttpd:nanohttpd-nanolets:2.3.1'

    /* JSON serialization */
    compile 'com.google.code.gson:gson:2.8.0'

    /* Protocol Buffers */
    compile 'com.google.protobuf:protobuf-lite:3.0.1'

    testCompile 'junit:junit:4.12'
}
